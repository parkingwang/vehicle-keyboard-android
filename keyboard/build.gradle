apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

group = GROUP

boolean isReleaseBuild = gradle.startParameter.taskNames.contains('bintrayUpload')
version = isReleaseBuild ? VERSION_NAME : "$VERSION_NAME-SNAPSHOT"
logger.info("VehicleKeyboardAndroid using version=$project.version")

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        consumerProguardFile 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.sourceCompatibility
    }
}

dependencies {
    api 'com.android.support:support-v4:25.1.0'

    api 'org.mozilla:rhino:1.7.7.2'
    api 'com.annimon:stream:1.1.9'

    testImplementation 'junit:junit:4.12'
}

// Create source/javadoc artifacts for publishing
apply from: "$rootDir/gradle/artifacts.gradle"

// Configure android-maven-gradle-plugin
install {
    repositories.mavenInstaller.pom.project {
        name POM_NAME
        description POM_DESCRIPTION
        packaging POM_PACKAGING
        url POM_URL

        licenses {
            license {
                name POM_LICENCE_NAME
                url POM_LICENCE_URL
                distribution POM_LICENCE_DIST
            }
        }

        scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
        }

        developers {
            developer {
                id 'yoojia'
                name 'Yoojia Chen'
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'

            if (project.hasProperty('bintrayUser')) {
                username = project.property('bintrayUser')
                password = project.property('bintrayKey')
            }
        }

        defaults {
            publishConfigs('archives')
        }
    }
}

// Configure gradle-bintray-plugin (for publishing releases)
bintray {
    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = 'VehicleKeyboardAndroid'
        userOrg = 'parkingwang'
        licenses = ['Apache-2.0']
        labels = ['VehicleNumber', 'keyboard', 'android']
        websiteUrl = 'https://github.com/parkingwang/vehicle-keyboard-android'
        issueTrackerUrl = 'https://github.com/parkingwang/vehicle-keyboard-android/issues'
        vcsUrl = 'https://github.com/parkingwang/vehicle-keyboard-android.git'
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    def sync = bintray.pkg.version.mavenCentralSync
    sync.sync = true
    sync.user = project.sonatypeUsername
    sync.password = project.sonatypePassword
}
